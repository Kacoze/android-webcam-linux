# Maintainer: Kacoze

pkgname=android-webcam-linux
pkgver=1.2.7
pkgrel=1
pkgdesc="Use Android phone as a Linux webcam via scrcpy + v4l2loopback"
arch=('any')
url="https://github.com/Kacoze/android-webcam-linux"
license=('MIT')
depends=('bash' 'android-tools' 'scrcpy' 'v4l2loopback-dkms' 'ffmpeg' 'libnotify' 'xorg-server-xvfb')

source=("${url}/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('SKIP')

prepare() {
  cd "${srcdir}/android-webcam-linux-${pkgver}"
}

package() {
  cd "${srcdir}/android-webcam-linux-${pkgver}"

  install -Dm755 src/android-webcam-ctl "${pkgdir}/usr/bin/android-webcam-ctl"
  install -Dm644 src/android-webcam-common "${pkgdir}/usr/bin/android-webcam-common"
  install -Dm755 src/android-webcam-run-in-terminal "${pkgdir}/usr/bin/android-webcam-run-in-terminal"

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm644 keys/minisign.pub "${pkgdir}/usr/share/${pkgname}/minisign.pub"

  install -Dm644 /dev/stdin "${pkgdir}/usr/share/applications/android-cam.desktop" <<'EOF'
[Desktop Entry]
Version=1.0
Name=Camera Phone
Comment=Toggle Android Camera
Exec=/usr/bin/android-webcam-ctl toggle
Path=/usr/bin
Icon=camera-web
Terminal=false
Type=Application
Categories=Utility;Video;
StartupWMClass=scrcpy
Actions=Status;Config;Setup;Stop;Logs;Update;

[Desktop Action Status]
Name=Check Status
Exec=/usr/bin/android-webcam-run-in-terminal status
Path=/usr/bin
Terminal=false

[Desktop Action Config]
Name=Settings
Exec=/usr/bin/android-webcam-run-in-terminal config
Path=/usr/bin
Terminal=false

[Desktop Action Setup]
Name=Setup (fix)
Exec=/usr/bin/android-webcam-run-in-terminal setup
Path=/usr/bin
Terminal=false

[Desktop Action Stop]
Name=Stop Camera
Exec=/usr/bin/android-webcam-ctl stop
Path=/usr/bin
Terminal=false

[Desktop Action Logs]
Name=Show Logs
Exec=/usr/bin/android-webcam-run-in-terminal logs
Path=/usr/bin
Terminal=false

[Desktop Action Update]
Name=Update
Exec=/usr/bin/android-webcam-run-in-terminal update
Path=/usr/bin
Terminal=false
EOF

  install -Dm644 /dev/stdin "${pkgdir}/usr/share/applications/android-cam-fix.desktop" <<'EOF'
[Desktop Entry]
Version=1.0
Name=Setup (fix)
Comment=Reconnect after restart
Exec=/usr/bin/android-webcam-run-in-terminal setup
Icon=smartphone
Terminal=false
Type=Application
Categories=Utility;Settings;
EOF

  install -Dm644 /dev/stdin "${pkgdir}/usr/share/android-webcam/VERSION" <<EOF
${pkgver}
EOF
}
